plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '0.4.10'
}

apply plugin: 'idea'

group 'org.jf'
version '0.6.2'

sourceCompatibility = 1.8

def pythonCoreVersion = "193.6494.42"

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'com.athaydes.rawhttp:rawhttp-core:2.2.1'
}

intellij {
    version '2019.3.3'
    plugins 'PythonCore:193.6494.42',
        'java'
    updateSinceUntilBuild false
}

task copyStubs(dependsOn: prepareSandbox, type: Copy) {
    from "src/main/python"
    into "${intellij.sandboxDirectory}/plugins/fusion_idea/lib"
}

jar {
    // This is a workaround for a change in behavior for how these dependency files for optional dependencies are specified.
    // in/aronud 2021.1.2, the behavior changed so that you have to specify the full path from the root of the plugin zip
    // to the file, vs. the old behavior of specifying a path relative to the META-INF folder.
    //
    // The workaround is to just use the same path as before, and make duplicate copies of the files in both the root
    // and the META-INF folder
    from("src/main/resources/META-INF") {
        include "intellij.xml"
        include "pycharm.xml"
    }
}

buildPlugin.dependsOn copyStubs
runIde.dependsOn copyStubs