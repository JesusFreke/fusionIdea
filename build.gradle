plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '1.13.2'
}

apply plugin: 'idea'

group 'org.jf'
version '0.6.8'

sourceCompatibility = 1.17

def pythonCoreVersion = "222.3345.131"

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'junit:junit:4.12'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'com.athaydes.rawhttp:rawhttp-core:2.2.1'
}

intellij {
    version = '2022.2'
    plugins =  ["PythonCore:$pythonCoreVersion", 'java']
    updateSinceUntilBuild = false
}

task copyStubs(dependsOn: prepareSandbox, type: Copy) {
    from "src/main/python"
    into "${intellij.sandboxDir.get()}/plugins/fusion_idea/lib"
}

jar {
    // This is a workaround for a change in behavior for how these dependency files for optional dependencies are specified.
    // in/aronud 2021.1.2, the behavior changed so that you have to specify the full path from the root of the plugin zip
    // to the file, vs. the old behavior of specifying a path relative to the META-INF folder.
    //
    // The workaround is to just use the same path as before, and make duplicate copies of the files in both the root
    // and the META-INF folder
    from("src/main/resources/META-INF") {
        include "intellij.xml"
        include "pycharm.xml"
    }
}

buildPlugin.dependsOn copyStubs
runIde.dependsOn copyStubs